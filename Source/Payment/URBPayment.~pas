unit URBPayment;

interface
{$I stbasis.inc}
uses
  Windows, Messages, SysUtils, Classes, Graphics, Controls, Forms, Dialogs,
  URBMainGrid, Db, IBCustomDataSet, IBQuery, DBCtrls, StdCtrls, Buttons,
  ExtCtrls, dbgrids, IBDatabase, IB, Menus , tsvDbGrid, IBUpdateSQL;

type
   TfmRBPayment = class(TfmRBMainGrid)
    procedure FormDestroy(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure bibRefreshClick(Sender: TObject);
    procedure bibAddClick(Sender: TObject);
    procedure bibChangeClick(Sender: TObject);
    procedure bibDelClick(Sender: TObject);
    procedure bibViewClick(Sender: TObject);
    procedure bibFilterClick(Sender: TObject);
  private
    isFindCard,isFindPurpose: Boolean;
    FindCard,FindPurpose: String;
  protected
    procedure GridDblClick(Sender: TObject); override;
    procedure SaveToIni;override;
    procedure LoadFromIni; override;
    function GetFilterString: string; override;
    procedure GridTitleClickWithSort(Column: TColumn; TypeSort: TTypeColumnSort); override;
    function GetSql: string; override;
  public
    procedure ActiveQuery(CheckPerm: Boolean);override;
  end;

var
  fmRBPayment: TfmRBPayment;

implementation

uses UMainUnited, UPaymentCode, UPaymentDM, UPaymentData, UEditRBPayment,
     tsvPicture;

{$R *.DFM}

procedure TfmRBPayment.FormCreate(Sender: TObject);
var
  cl: TColumn;
begin
 inherited;
 try
  Caption:=NameRbkPayment;

  Mainqr.Database:=IBDB;
  IBTran.AddDatabase(IBDB);
  IBDB.AddTransaction(IBTran);
  
  cl:=Grid.Columns.Add;
  cl.FieldName:='num_card';
  cl.Title.Caption:='Карта оплаты';
  cl.Width:=150;

  cl:=Grid.Columns.Add;
  cl.FieldName:='purpose_name';
  cl.Title.Caption:='Назначение';
  cl.Width:=150;

  cl:=Grid.Columns.Add;
  cl.FieldName:='how_much';
  cl.Title.Caption:='Сумма платежа';
  cl.Width:=50;

  cl:=Grid.Columns.Add;
  cl.FieldName:='date_payment';
  cl.Title.Caption:='Дата и время';
  cl.Width:=110;

  LoadFromIni;
 except
  {$IFDEF DEBUG} on E: Exception do Assert(false,E.message); {$ENDIF}
 end;
end;

procedure TfmRBPayment.FormDestroy(Sender: TObject);
begin
  inherited;
  if FormState=[fsCreatedMDIChild] then
   fmRBPayment:=nil;
end;

function TfmRBPayment.GetSql: string;
begin
  Result:= inherited GetSql;
  if Trim(Result)<>'' then exit;
  Result:=SQLRbkPayment+GetFilterString+GetLastOrderStr;
end;

procedure TfmRBPayment.ActiveQuery(CheckPerm: Boolean);
var
 sqls: String;
begin
 try
  Mainqr.Active:=false;
  if CheckPerm then
   if not CheckPermission then exit;

  Screen.Cursor:=crHourGlass;
  Mainqr.DisableControls;
  try
   Mainqr.sql.Clear;
   sqls:=GetSql;
   Mainqr.sql.Add(sqls);
   Mainqr.Transaction.Active:=false;
   Mainqr.Transaction.Active:=true;
   Mainqr.Active:=true;
   SetImageFilter(isFindCard or isFindPurpose);
   ViewCount;
  finally
   Mainqr.EnableControls;
   Screen.Cursor:=crDefault;
  end;
 except
  {$IFDEF DEBUG} on E: Exception do Assert(false,E.message); {$ENDIF}
 end;
end;

procedure TfmRBPayment.GridTitleClickWithSort(Column: TColumn; TypeSort: TTypeColumnSort);
var
  fn: string;
  id: string;
begin
 try
   if not MainQr.Active then exit;
   fn:=Column.FieldName;
   if AnsiUpperCase(fn)=AnsiUpperCase('num_card') then fn:='c.num_card';
   if AnsiUpperCase(fn)=AnsiUpperCase('purpose_name') then fn:='pp.name';
   id:=MainQr.fieldByName('payment_id').asString;
   SetLastOrderFromTypeSort(fn,TypeSort);
   ActiveQuery(false);
   MainQr.First;
   MainQr.Locate('payment_id',id,[loCaseInsensitive]);
 except
  {$IFDEF DEBUG} on E: Exception do Assert(false,E.message); {$ENDIF}
 end;
end;

procedure TfmRBPayment.GridDblClick(Sender: TObject);
begin
  if not Mainqr.Active then exit;
  if Mainqr.RecordCount=0 then exit;
  if pnSQL.Visible and bibChange.Enabled then begin
   bibChange.Click;
  end else bibView.Click;
end;

procedure TfmRBPayment.LoadFromIni;
begin
 inherited;
 try
    FindCard:=ReadParam(ClassName,'card',FindCard);
    FindPurpose:=ReadParam(ClassName,'purpose',FindPurpose);
    FilterInside:=ReadParam(ClassName,'Inside',FilterInside);
 except
   {$IFDEF DEBUG} on E: Exception do Assert(false,E.message); {$ENDIF}
 end;
end;

procedure TfmRBPayment.SaveToIni;
begin
 inherited;
 try
    WriteParam(ClassName,'card',FindCard);
    WriteParam(ClassName,'purpose',FindPurpose);
    WriteParam(ClassName,'Inside',FilterInside);
 except
  {$IFDEF DEBUG} on E: Exception do Assert(false,E.message); {$ENDIF}
 end;
end;

procedure TfmRBPayment.bibRefreshClick(Sender: TObject);
begin
  ActiveQuery(true);
end;

procedure TfmRBPayment.bibAddClick(Sender: TObject);
var
  fm: TfmEditRBPayment;
begin
  if not Mainqr.Active then exit;
  fm:=TfmEditRBPayment.Create(nil);
  try
    fm.fmParent:=Self;
    fm.TypeEditRBook:=terbAdd;
    fm.ChangeFlag:=false;
    if fm.ShowModal=mrok then begin
     ViewCount;
     MainQr.Locate('payment_id',fm.oldpayment_id,[loCaseInsensitive]);
    end;
  finally
    fm.Free;
  end;
end;

procedure TfmRBPayment.bibChangeClick(Sender: TObject);
var
  fm: TfmEditRBPayment;
  TPRBI: TParamRBookInterface;
  card_nominal: Integer;
begin
  if not Mainqr.Active then exit;
  if Mainqr.IsEmpty then exit;
  fm:=TfmEditRBPayment.Create(nil);
  try
    fm.fmParent:=Self;
    fm.TypeEditRBook:=terbChange;
    fm.edCard.Text:=Mainqr.fieldByName('num_card').AsString;
    fm.card_id:=Mainqr.fieldByName('card_id').AsInteger;
    fm.edPurpose.Text:=Mainqr.fieldByName('purpose_name').AsString;
    fm.purpose_id:=Mainqr.fieldByName('purpose_id').AsInteger;
    fm.edHowMuch.Text:=Mainqr.fieldByName('how_much').AsString;
    fm.dtpDate.Date:=Mainqr.fieldByName('date_payment').AsDateTime;
    fm.dtpTime.Time:=Mainqr.fieldByName('date_payment').AsDateTime;
    fm.oldpayment_id:=MainQr.FieldByName('payment_id').AsInteger;

    FillChar(TPRBI,SizeOf(TPRBI),0);
    TPRBI.Visual.TypeView:=tviOnlyData;
    TPRBI.Condition.WhereStr:=PChar(' card_id='+inttostr(fm.card_id)+' ');
    if _ViewInterfaceFromName(NameRbkCard,@TPRBI) then begin
      card_nominal:=GetFirstValueFromParamRBookInterface(@TPRBI,'nominal');
      fm.lbLimit.Caption:=Format('Остаток: %d',[card_nominal-fm.GetLimitByCard]);
    end;

    fm.ChangeFlag:=false;
    if fm.ShowModal=mrok then begin
       MainQr.Locate('payment_id',fm.oldpayment_id,[loCaseInsensitive]);
    end;
  finally
    fm.Free;
  end;
end;

procedure TfmRBPayment.bibDelClick(Sender: TObject);
var
  but: Integer;

  function DeleteRecord: Boolean;
  var
    qr: TIBQuery;
    tran: TIBTransaction;
    sqls: string;
  begin
   Screen.Cursor:=crHourGlass;
   qr:=TIBQuery.Create(nil);
   tran:=TIBTransaction.Create(nil);
   try
    result:=false;
    try
     tran.AddDatabase(IBDB);
     IBDB.AddTransaction(tran);
     tran.Params.Text:=DefaultTransactionParamsTwo;
     qr.Database:=IBDB;
     qr.Transaction:=tran;
     qr.Transaction.Active:=true;
     sqls:='Delete from '+tbPayment+' where payment_id='+
          Mainqr.FieldByName('payment_id').asString;
     qr.sql.Add(sqls);
     qr.ExecSQL;
     qr.Transaction.Commit;

     IBUpd.DeleteSQL.Clear;
     IBUpd.DeleteSQL.Add(sqls);
     Mainqr.Delete;
     
     ViewCount;

     Result:=true;
    except
     on E: EIBInterBaseError do begin
        TempStr:=TranslateIBError(E.Message);
        ShowErrorEx(TempStr);
        Assert(false,TempStr);
     end;
     {$IFDEF DEBUG} on E: Exception do Assert(false,E.message); {$ENDIF}
    end;
   finally
    tran.Free; 
    qr.Free;
    Screen.Cursor:=crDefault;
   end;

  end;

begin
  if Mainqr.RecordCount=0 then exit;
  but:=DeleteWarningEx('оплату по карте <'+Mainqr.FieldByName('num_card').AsString+
                       '> за '+'<'+Mainqr.FieldByName('date_payment').AsString+'>'+'?');
  if but=mrYes then begin
    if not deleteRecord then begin
    end;
  end;
end;

procedure TfmRBPayment.bibViewClick(Sender: TObject);
var
  fm: TfmEditRBPayment;
begin
  if not Mainqr.Active then exit;
  if Mainqr.IsEmpty then exit;
  fm:=TfmEditRBPayment.Create(nil);
  try
    fm.TypeEditRBook:=terbView;
    fm.edCard.Text:=Mainqr.fieldByName('num_card').AsString;
    fm.card_id:=Mainqr.fieldByName('card_id').AsInteger;
    fm.edPurpose.Text:=Mainqr.fieldByName('purpose_name').AsString;
    fm.purpose_id:=Mainqr.fieldByName('purpose_id').AsInteger;
    fm.edHowMuch.Text:=Mainqr.fieldByName('how_much').AsString;
    fm.dtpDate.Date:=Mainqr.fieldByName('date_payment').AsDateTime;
    fm.dtpTime.Time:=Mainqr.fieldByName('date_payment').AsDateTime;
    fm.oldpayment_id:=MainQr.FieldByName('payment_id').AsInteger;
    if fm.ShowModal=mrok then begin
    end;
  finally
    fm.Free;
  end;
end;

procedure TfmRBPayment.bibFilterClick(Sender: TObject);
var
  fm: TfmEditRBPayment;
  filstr: string;
begin
 fm:=TfmEditRBPayment.Create(nil);
 try
  fm.TypeEditRBook:=terbFilter;

  fm.edCard.ReadOnly:=false;
  fm.edCard.Color:=clWindow;

  fm.edPurpose.ReadOnly:=false;
  fm.edPurpose.Color:=clWindow;

  if Trim(FindCard)<>'' then fm.edCard.Text:=FindCard;
  if Trim(FindPurpose)<>'' then fm.edPurpose.Text:=FindPurpose;

  fm.dtpDate.Enabled:=false;
  fm.dtpDate.Color:=clBtnFace;
  fm.lbDateTime.Enabled:=false;
  fm.dtpTime.Enabled:=false;
  fm.dtpTime.Color:=clBtnFace;

  fm.meNote.Enabled:=false;
  fm.meNote.Color:=clBtnFace;
  fm.lbNote.Enabled:=false;

  fm.edHowMuch.Enabled:=false;
  fm.edHowMuch.Color:=clBtnFace;
  fm.lbHowMuch.Enabled:=false;

  fm.cbInString.Checked:=FilterInSide;

  fm.ChangeFlag:=false;

  if fm.ShowModal=mrOk then begin

    inherited;

    FindCard:=Trim(fm.edCard.Text);
    FindPurpose:=Trim(fm.edPurpose.Text);

    FilterInSide:=fm.cbInString.Checked;
    if FilterInSide then filstr:='%';

    ActiveQuery(false);
    ViewCount;
  end;
 finally
  fm.Free;
 end;
end;

function TfmRBPayment.GetFilterString: string;
var
  FilInSide: string;
  wherestr: string;
  addstr1,addstr2: string;
  and1: string;
begin
    Result:=Inherited GetFilterString;
    if Trim(Result)<>'' then exit;

    isFindCard:=Trim(FindCard)<>'';
    isFindPurpose:=Trim(FindPurpose)<>'';

    if isFindCard or isFindPurpose then begin
     wherestr:=' where ';
    end else begin
    end;

    if FilterInside then FilInSide:='%';

     if isFindCard then begin
        addstr1:=' Upper(c.num_card) like '+AnsiUpperCase(QuotedStr(FilInSide+FindCard+'%'))+' ';
     end;

     if isFindPurpose then begin
        addstr2:=' Upper(pp.name) like '+AnsiUpperCase(QuotedStr(FilInSide+FindPurpose+'%'))+' ';
     end;

     if (isFindCard and isFindPurpose) then
      and1:=' and ';

     Result:=wherestr+addstr1+and1+
                      addstr2;
end;

end.
